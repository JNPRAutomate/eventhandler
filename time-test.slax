version 1.0;
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns ext = "http://xmlsoft.org/XSLT/namespace";
ns date = "http://exslt.org/dates-and-times";
import "/var/db/scripts/import/junos.xsl";

/* convert month to int */
template month-name-int($name) {
  if ($name == "Jan") {
    <result> "01";
  } else if ($name == "Feb") {
    <result> "02";
  } else if ($name == "Mar") {
    <result> "03";
  } else if ($name == "Apr") {
    <result> "04";
  } else if ($name == "May") {
    <result> "05";
  } else if ($name == "Jun") {
    <result> "06";
  } else if ($name == "Jul") {
    <result> "07";
  } else if ($name == "Aug") {
    <result> "08";
  } else if ($name == "Sep") {
    <result> "09";
  } else if ($name == "Oct") {
    <result> "10";
  } else if ($name == "Nov") {
    <result> "11";
  } else if ($name == "Dec") {
    <result> "12";
  } else {
    /* No match return empty */
    <result> "";
  }
}

/* pad a number with a 0 if it is less than 2 char in length */
template lpad-zero($date){
  if (string-length($date) < 2) {
    <result> "0" _ $date;
  } else {
    <result> $date;
  }
}

/* convert a timestamp found in show-chassis-cluster-information to iso8601 format */
template jdate-iso8601($date) {
  /* Expected format "Jul 11 13:44:09.650" */
  var $sane = jcs:regex("([A-Za-z]{3}) ([0-9]+) ([0-9]+:[0-9]+:[0-9]+)",normalize-space($date));
  var $month = {
    call month-name-int($name = $sane[2]);
  }
  var $day = {
    call lpad-zero() {
      with $date = $sane[3];
    }
  }
  var $time = $sane[4];
  var $year = date:year();
  var $isofmt = $year _ "-" _ $month _ "-" _ $day _ "T" _ $time _ "Z";
  <result> $isofmt;
}

/* template that checks if the diff between two times is within the specified diff */
/* $date1 and $date2 must be in ISO8601 format */
/* $diff is in seconds */
template compare-date($date1,$date2,$diff){
  var $d1seconds = date:seconds($date1);
  var $d2seconds = date:seconds($date2);
  var $calcdiff = $d2seconds - $d1seconds;
  if (($calcdiff <= $diff) and ($calcdiff >= 0)) { /* check both sides of the bounds */
    /* The times are within the diff */
    <result> true();
  } else {
    /* returning false because both conditions failed */
    <result> false();
  }
}

match / {
  <op-script-results> {

    var $test-time1 = "Jul 17 18:44:04.776";
    var $isofmt1 = {
      call jdate-iso8601($date = $test-time1);
    }
    var $test-time2 = "Jul 17 18:44:33.776";
    var $isofmt2 = {   
      call jdate-iso8601($date = $test-time2);  
    }  
    var $diftest1 = {
      call compare-date($date1 = $isofmt1,$date2 = $isofmt2,$diff = 30);
    }

    var $test-time3 = "Jul 17 18:44:04.776";
    var $isofmt3 = {
      call jdate-iso8601($date = $test-time3);
    }
    var $test-time4 = "Jul 17 18:44:34.776";
    var $isofmt4 = {   
      call jdate-iso8601($date = $test-time4);  
    }  
    var $diftest2 = {
      call compare-date($date1 = $isofmt3,$date2 = $isofmt4,$diff = 30);
    }

    var $test-time5 = "Jul 17 18:44:04.776";
    var $isofmt5 = {
      call jdate-iso8601($date = $test-time5);
    }
    var $test-time6 = "Jul 17 18:44:35.776";
    var $isofmt6 = {   
      call jdate-iso8601($date = $test-time6);  
    }  
    var $diftest3 = {
      call compare-date($date1 = $isofmt5,$date2 = $isofmt6,$diff = 30);
    }

    var $test-time7 = "Jul 17 18:44:04.776";
    var $isofmt7 = {
      call jdate-iso8601($date = $test-time7);
    }
    var $test-time8 = "Jul 17 18:43:04.776";
    var $isofmt8 = {   
      call jdate-iso8601($date = $test-time8);  
    }  
    var $diftest4 = {
      call compare-date($date1 = $isofmt7,$date2 = $isofmt8,$diff = 30);
    }

    var $test-time9 = "Jul 17 18:44:04.776";
    var $isofmt9 = {
      call jdate-iso8601($date = $test-time9);
    }
    var $test-time10 = "Jul 17 18:43:04.776";
    var $isofmt10 = {   
      call jdate-iso8601($date = $test-time10);  
    }  
    var $diftest5 = {
      call compare-date($date1 = $isofmt9,$date2 = $isofmt10,$diff = 30);
    }

    <output> $isofmt1;
    <output> $isofmt2;
    <output> "Should be true";
    <output> $diftest1;
    <output> $isofmt3;
    <output> $isofmt4;
    <output> "Should be true";
    <output> $diftest2;
    <output> $isofmt5;
    <output> $isofmt6;
    <output> "Should be false";
    <output> $diftest3;
    <output> "Should be false";
    <output> $diftest4;
    <output> "Should be false";
    <output> $diftest5;
    <output> date:date-time();
    <output> normalize-space($localtime);
  }
}
